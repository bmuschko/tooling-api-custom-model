buildscript {
    repositories {
        maven { 
            url "$projectDir/../repo"
        }
    }
    
    dependencies {
        classpath 'org.gradle.sample.plugins.toolingapi:model:1.0'
    }
}

import org.gradle.tooling.GradleConnector
import org.gradle.tooling.ProjectConnection
import org.gradle.tooling.ModelBuilder
import org.gradle.sample.plugins.toolingapi.custom.CustomModel

task checkJavaPlugin {
    doLast {
        GradleConnector connector = GradleConnector.newConnector()
        connector.forProjectDirectory(file("sample"))
        ProjectConnection connection
        
        try {
            connection = connector.connect()
            ModelBuilder<CustomModel> customModelBuilder = connection.model(CustomModel.class)
            customModelBuilder.withArguments("--init-script", "$projectDir/init.gradle")  
            CustomModel model = customModelBuilder.get()         
            assert model.hasPlugin(JavaPlugin.class)
        } finally {
            connection?.close()
        }
    }
}